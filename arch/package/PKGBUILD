# Maintainer: stazed<stazed@mapson.com>
_pkgbasename=seq32
pkgname=${_pkgbasename}-git
pkgver=1.1.5
pkgrel=1
pkgdesc="A live-looping MIDI sequencer"
arch=('i686' 'x86_64')
url="http//github.com/Stazed/seq32.git"
license=('GPL')
depends=('gtkmm' 'jack' 'liblo')
makedepends=('git')
provides=("${_pkgbasename}")
conflicts=("${_pkgbasename}")
source=("${_pkgbasename}::git://github.com/Stazed/seq32.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgbasename}"

    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}/${_pkgbasename}"

    autoreconf -i
    ./configure --prefix=/usr --disable-lash
    make
}

package() {
    cd "${srcdir}/${_pkgbasename}"

    make DESTDIR="${pkgdir}" install

}
